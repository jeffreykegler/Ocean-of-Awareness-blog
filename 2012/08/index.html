<html>
<head>
<link rel="alternate" type="type="application/rss+xml" title="RSS" href="http://jeffreykegler.github.com/Ocean-of-Awareness-blog/index.rss" />
<title>Ocean of Awareness   </title>
<style type="text/css">
   strong {font-weight: 700;}
</style>
</head>
<body>
<div id="header"
  style="color:white;background-color:#38B0C0;padding:1em;clear:left;text-align:center;">
<h1>Ocean of Awareness</h1>
  
</div>
  <div id="menu" style="margin:0;padding:1em;width:100px;float:left;">
  <h2>Jeffrey Kegler's blog</h2>
  <p>About Marpa, his new parsing algorithm,
    and other topics of interest</p>
  </div>
  <div id="content" style="margin-left:190px;border-left:2px solid #38B0C0;padding:1em;">
<h3>Sat, 25 Aug 2012</h3>
<br />
<center><a name="announce"> <h2>A new home for the Ocean of Awareness blog</h2> </a>
</center>
<a href="blogs.perl.org">blogs.perl.org</a>
has been a wonderful home for my
Ocean of Awareness blog.
But the call of static blogging is becoming impossible to resist.
This may become the new home
of the Ocean of Awareness blog.
<br />
<p>posted at: 17:22 |
path: <a href="http://jeffreykegler.github.com/Ocean-of-Awareness-blog"></a> |
<a href="http://jeffreykegler.github.com/Ocean-of-Awareness-blog/2012/08/25#announce">permanent link to this entry</a>
</p>
<div style="margin:0;height:20px;font-size:xx-large;color:#38B0C0;padding:1px;text-align:center;">
&bull;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
&bull;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
&bull;
</div>
<br />
<center><a name="dsl"> <h2>DRAFT: Domain-Specific Languages made simple</h2> </a>
</center>
<h2>Writing your own language</h2>
<p>Creating your own languages has been A Big Deal(tm).
What if you could create a simple language in hours or minutes?
But there has been a serious obstacle to this up to now.
No practical parser "just parsed" BNF.
With Marpa, that restriction is lifted.
<p>
In this post, I will describe
a small, sample Marpa domain-specific language (DSL).
In designing it I am inspired by Mark Dominus's
description of the
"Worse is Better" philosophy,
and Blosxom's implementation of it.
This DSL is kept very simple.
The intent is that the reader will build
on it.
<h2>A calculator</h2>
<p>
This DSL is a calculator.
Calculators are a familiar example.
And, after all, whatever tool you build this
DSL into, it will probably be useful
to have a calculator as part of it.
What follows contains only the parts of the code
relevant to the discussion.
The full code is a Github gist.
<p>
<h2>The grammar</h2>
<p>Marpa allows you to build the grammar in a clean, sensible structure,
with a separate grammar, tokenize and semantics.
If you're used to doing parsing with regexes or recursive descent,
you expect to see them mixed together and this cleaner approach make
may you uneasy --
not without reason, since in the past the tools that used
it were painful to use and often would not deliver.
<p>Here's the grammar for our calculator.
<div style="white-space:pre;overflow:auto;font-family:monospace;margin:0;padding:1em 0 1em 2.8em;">

require './OP2.pm';    ## no critic (Modules::RequireBarewordIncludes)

my $rules = Marpa::Demo::OP2::parse_rules(
    <<'END_OF_GRAMMAR'
reduce_op ::= '+' | '-' | '/' | '*'
script ::= e
script ::= script ';' e => do_arg2
e ::=
     NUM
   | VAR => do_is_var
   | :group '(' e ')' => do_arg1
  || '-' e => do_negate
  || :right e '^' e => do_binop
  || e '*' e => do_binop
   | e '/' e => do_binop
  || e '+' e => do_binop
   | e '-' e => do_binop
  || e ',' e => do_array
  || reduce_op 'reduce' e => do_reduce
  || VAR '=' e => do_set_var
END_OF_GRAMMAR
);

</div>
<div style="white-space:pre;overflow:auto;font-family:monospace;margin:0;padding:1em 0 1em 2.8em;">
my $grammar = Marpa::R2::Grammar->new(
    {   start          => 'script',
        actions        => __PACKAGE__,
        default_action => 'add_brackets',
        rules          => $rules,
    }
);
$grammar->precompute;

# Order matters !!
my @terminals = (
    [ q{'reduce'}, qr/reduce\b/xms ],
    [ 'NUM',  qr/\d+/xms ],
    [ 'VAR',  qr/\w+/xms ],
    [ q{'='}, qr/[=]/xms ],
    [ q{';'}, qr/[;]/xms ],
    [ q{'*'}, qr/[*]/xms ],
    [ q{'/'}, qr/[\/]/xms ],
    [ q{'+'}, qr/[+]/xms ],
    [ q{'-'}, qr/[-]/xms ],
    [ q{'^'}, qr/[\^]/xms ],
    [ q{'('}, qr/[(]/xms ],
    [ q{')'}, qr/[)]/xms ],
    [ q{','}, qr/[,]/xms ],
);

our $DEBUG = 1;

my %binop_closure = (
    '*' => sub { $_[0] * $_[1] },
    '/' => sub { $_[0] / $_[1] },
    '+' => sub { $_[0] + $_[1] },
    '-' => sub { $_[0] - $_[1] },
    '^' => sub { $_[0] ** $_[1] },
);

</div>
<h2>The Semantics</h2>
<p>The semantics for this calculator are somewhat interesting,
but this post is about <strong>your</strong> interesting
semantics, not mine.
So I'll move on.
<p>
<div style="white-space:pre;overflow:auto;font-family:monospace;margin:0;padding:1em 0 1em 2.8em;">

sub calculate {
    my ($string) = @_;
    my $rec = Marpa::R2::Recognizer->new( { grammar => $grammar } );

    %symbol_table = ();

    my $length = length $string;
    pos $string = 0;
    TOKEN: while ( pos $string < $length ) {

        # skip whitespace
        next TOKEN if $string =~ m/\G\s+/gcxms;

        # read other tokens
        TOKEN_TYPE: for my $t (@terminals) {
            next TOKEN_TYPE if not $string =~ m/\G($t->[1])/gcxms;
            if ( not defined $rec->read( $t->[0], $1 ) ) {
                say $rec->show_progress() or die "say failed: $ERRNO";
                my $problem_position = ( pos $string ) - length $1;
                my $before_start     = $problem_position - 40;
                $before_start = 0 if $before_start < 0;
                my $before_length = $problem_position - $before_start;
                die "Problem near position $problem_position\n",
                    q{Problem is here: "},
                    ( substr $string, $before_start, $before_length + 40 ),
                    qq{"\n},
                    ( q{ } x ( $before_length + 18 ) ), qq{^\n},
                    q{Token rejected, "}, $t->[0], qq{", "$1"},
                    ;
            } ## end if ( not defined $rec->read( $t->[0], $1 ) )
            next TOKEN;
        } ## end TOKEN_TYPE: for my $t (@terminals)

        die q{No token at "}, ( substr $string, pos $string, 40 ),
            q{", position }, pos $string;
    } ## end TOKEN: while ( pos $string < $length )

    $rec->end_input;

    my $value_ref = $rec->value;

    if ( !defined $value_ref ) {
        say $rec->show_progress() or die "say failed: $ERRNO";
        die 'Parse failed';
    }
    return ${$value_ref};

} ## end sub calculate

sub report_calculation {
    my ($string) = @_;
    my $output = qq{Input: "$string"\n};
    my $result = calculate($string);
    $result = join q{,}, @{$result} if ref $result eq 'ARRAY';
    $output .= "  Parse: $result\n";
    for my $symbol (sort keys %symbol_table) {
        $output .= qq{"$symbol" = "} . $symbol_table{$symbol} . qq{"\n};
    }
    return $output;
}

if (@ARGV) {
    my $result = calculate(join ';', grep { /\S/ } @ARGV);
    $result = join q{,}, @{$result} if ref $result eq 'ARRAY';
    say "Result is ", $result;
    for my $symbol (sort keys %symbol_table) {
        say qq{"$symbol" = "} . $symbol_table{$symbol} . qq{"};
    }
    exit 0;
}

</div>
<h2>Alernatives</h2>
<p>This calculator uses
Marpa::R2.
Marpa::R2 is beta,
while Marpa::XS is in a stable, bug-fix only release.
On the other hand Marpa::R2 is somewhat faster here,
and its reporting of parse-time problems is cleaner.
<br />
<p>posted at: 06:25 |
path: <a href="http://jeffreykegler.github.com/Ocean-of-Awareness-blog"></a> |
<a href="http://jeffreykegler.github.com/Ocean-of-Awareness-blog/2012/08/25#dsl">permanent link to this entry</a>
</p>
<div style="margin:0;height:20px;font-size:xx-large;color:#38B0C0;padding:1px;text-align:center;">
&bull;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
&bull;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
&bull;
</div>
</div>
</div>
<div id="footer" style="color:white;background-color:#38B0C0;clear:left;padding:1em;">
</div>
<script src="javascripts/scale.fix.js"></script>
              <script type="text/javascript">
            var gaJsHost = (("https:" == document.location.protocol) ? "https://ssl." : "http://www.");
            document.write(unescape("%3Cscript src='" + gaJsHost + "google-analytics.com/ga.js' type='text/javascript'%3E%3C/script%3E"));
          </script>
          <script type="text/javascript">
            try {
              var pageTracker = _gat._getTracker("UA-33430331-1");
            pageTracker._trackPageview();
            } catch(err) {}
          </script>
</body>
</html>
